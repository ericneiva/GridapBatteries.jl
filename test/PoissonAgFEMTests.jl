module PoissonAgFEMTests

  using Gridap
  using GridapEmbedded
  using Test

  u(x) = x[1] - x[2]
  f(x) = -Δ(u)(x)

  R = 0.5
  L = 0.8*(2*R)
  p1 = Point(0.0,0.0)
  p2 = p1 + VectorValue(L,0.0)

  geo1 = disk(R,x0=p1)
  geo2 = disk(R,x0=p2)
  geo3 = setdiff(geo1,geo2)

  t = 1.01
  pmin = p1-t*R
  pmax = p1+t*R

  n = 30
  partition = (n,n)
  bgmodel = CartesianDiscreteModel(pmin,pmax,partition)
  dp = pmax - pmin
  h = dp[1]/n

  cutgeo = cut(bgmodel,geo3)

  strategy = AggregateAllCutCells()
  aggregates = aggregate(strategy,cutgeo)

  Ω_bg = Triangulation(bgmodel)
  Ω_act = Triangulation(cutgeo,ACTIVE)
  Ω = Triangulation(cutgeo,PHYSICAL)
  Γ = EmbeddedBoundary(cutgeo)

  n_Γ = get_normal_vector(Γ)

  dim = 3
  order = 1
  degree = 2*order*dim
  dΩ = Measure(Ω,degree)
  dΓ = Measure(Γ,degree)

  reffe = ReferenceFE(lagrangian,Float64,order)
  Vstd = TestFESpace(Ω_act,reffe)
  V = AgFEMSpace(Vstd,aggregates)
  U = TrialFESpace(V)

  γd = 10.0

  a(u,v) =
    ∫( ∇(v)⋅∇(u) ) * dΩ +
    ∫( (γd/h)*v*u  - v*(n_Γ⋅∇(u)) - (n_Γ⋅∇(v))*u ) * dΓ

  l(v) =
    ∫( v*f ) * dΩ +
    ∫( (γd/h)*v*u - (n_Γ⋅∇(v))*u ) * dΓ

  op = AffineFEOperator(a,l,U,V)
  uh = solve(op)

  e = u - uh

  l2(u) = sqrt(sum( ∫( u*u )*dΩ ))
  h1(u) = sqrt(sum( ∫( u*u + ∇(u)⋅∇(u) )*dΩ ))

  el2 = l2(e)
  eh1 = h1(e)
  ul2 = l2(uh)
  uh1 = h1(uh)

  #colors = color_aggregates(aggregates,bgmodel)
  #writevtk(Ω_bg,"trian",celldata=["aggregate"=>aggregates,"color"=>colors],cellfields=["uh"=>uh])
  writevtk(Ω,"trian_O",cellfields=["uh"=>uh])
  writevtk(Γ,"trian_G")
  @test el2/ul2 < 1.e-8
  @test eh1/uh1 < 1.e-7

end # module